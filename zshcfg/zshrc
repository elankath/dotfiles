# See ZSH startup files at http://zsh.sourceforge.net/Intro/intro_3.html
# zshrc is sourced in interactive shells
# It should be used to setup aliases, functions, options
# key-bindings, etc
#

[[ -z $dotfiles ]] && echo "ERROR: dotfiles variable not set! Check zshenv"

# http://dougblack.io/words/zsh-vi-mode.html
set -o vi
bindkey -v
# http://dougblack.io/words/zsh-vi-mode.html
export KEYTIMEOUT=1 # very important for lag killing.

# The behaviour of / and ? in command line is screwed up by prezto. I want to
# remove the incremental search keybindings 
# See
# http://unix.stackexchange.com/questions/285208/how-to-remove-a-zsh-keybinding-if-i-dont-know-what-it-does
# See man zshzle for these functions
bindkey -M vicmd "?" vi-history-search-forward
bindkey -M vicmd "/" vi-history-search-backward

#  Increase history size
## See http://zsh.sourceforge.net/Guide/zshguide02.html#l16
if [ -z "$HISTFILE" ]; then
    HISTFILE=$HOME/.zhistory
fi
export HISTFILESIZE=10000000
export SAVEHIST=1000000
export HISTSIZE=1000000
setopt HIST_ALLOW_CLOBBER  EXTENDED_HISTORY INC_APPEND_HISTORY
setopt HIST_IGNORE_DUPS HIST_REDUCE_BLANKS HIST_IGNORE_SPACE HIST_VERIFY 
setopt AUTOCD
setopt CD_ABLE_VARS
setopt PROMPT_SUBST
setopt PUSHD_IGNORE_DUPS AUTOPUSHD
setopt CORRECT
setopt EXTENDED_GLOB
setopt HASH_ALL
setopt INTERACTIVECOMMENTS


# See https://github.com/rupa/z/pull/200
[[ $isWsl ]] && { echo "Running in WSL. unset BG_NICE"; unsetopt BG_NICE }

# Add my zfuncs to fpath
# See 9.1 Autoloading Functions at http://zsh.sourceforge.net/Doc/Release/Functions.html
fpath=($dotfiles/zshcfg/zfuncs $fpath)

# Autoloads 
# See https://unix.stackexchange.com/questions/33255/how-to-define-and-load-your-own-shell-function-in-zsh
autoload -Uz compinit #Used for compdef function. 
autoload -U promptinit && promptinit
autoload -U colors && colors
# See http://www.refining-linux.org/archives/36/ZSH-Gem-1-Programmable-file-renaming/
autoload -U zmv
alias mmv='noglob zmv -W' #http://www.mfasold.net/blog/2008/11/moving-or-renaming-multiple-files/
# Load the  _curl completion function defined in zshcfg/zfuns. Was generated by curl/scripts/zsh.pl
autoload -Uz _curl
# VCS info for git propmt
#autoload -Uz vcs_info

# zstyle
#zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' list-prompt '%S%M matches%s' #from prezto
zstyle ':completion:*' menu select

[ $isCygwin ] && echo "Loading autoruns/completions. This can take time on Cygwin..."
# https://stackoverflow.com/questions/14677936/source-multiple-files-in-zshrc-with-wildcard
if [[ -d $dotfiles ]] ; then
  for file in $dotfiles/zshcfg/autorun/*.{sh,zsh}; do
    if [[ -f $file ]] ; then
      source "$file"
    fi
  done
fi

# TODO: the below doesn't appear to work on Cygwin. Fix ME.
# See http://zsh.sourceforge.net/Doc/Release/Completion-System.html#Initialization
# See https://gist.github.com/ctechols/ca1035271ad134841284
# On slow systems, checking the cached .zcompdump file to see if it must be 
# regenerated adds a noticable delay to zsh startup.  This little hack restricts 
# it to once a day.  It should be pasted into your own completion file.
#
# The globbing is a little complicated here:
# - '#q' is an explicit glob qualifier that makes globbing work within zsh's [[ ]] construct.
# - 'N' makes the glob pattern evaluate to nothing when it doesn't match (rather than throw a globbing error)
# - '.' matches "regular files"
# - 'mh+24' matches files (or directories or whatever) that are older than 24 hours.
autoload -Uz compinit 
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
	compinit -u;
else
	compinit -C;
fi;

command -v fortune 2>&1 >/dev/null && command -v cowsay 2>&1 >/dev/null && fortune | cowsay

if [[ $isCygwin && -d ~/.zplug/repos/ericdwang/zsh-lightrise ]]; then
  source ~/.zplug/repos/ericdwang/zsh-lightrise/zsh-lightrise.zsh 
  cd ~
fi
